apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion Config.compileSdkVersion
  buildToolsVersion Config.buildToolsVersion

  defaultConfig {
    minSdkVersion Config.minSdkVersion
    targetSdkVersion Config.targetSdkVersion
    missingDimensionStrategy 'abi', 'abi_all', 'abi_arm', 'abi_arm64', 'abi_x86'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    exclude 'META-INF/proguard/androidx-annotations.pro'
  }

  sourceSets {
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  useLibrary 'android.test.runner'
  useLibrary 'android.test.base'
}

dependencies {
  api project(':adblock-android')
  api Deps.androidx_preference
  testImplementation Deps.junit4
  testImplementation Deps.kotlin_stdlib
  androidTestImplementation Deps.kotlin_stdlib
  androidTestImplementation Deps.androidx_test_core
  androidTestImplementation Deps.androidx_test_runner
  androidTestImplementation Deps.androidx_test_ext_junit
}

// Add "android.jar" to javadoc plugin classpath (See DP-612)
afterEvaluate {
  def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
  tasks.withType(Javadoc).all {
    classpath += project.files(androidJar)
  }
}

// DP-1771 was created as the bintray-release plugin is deprecated
publish {
  userOrg = Bintray.userOrg
  groupId = Bintray.groupId
  artifactId = 'adblock-android-settings'
  publishVersion = Config.moduleVersion
  licences = Bintray.licences
  desc = 'An Android library that provides a configuration interface for Adblock Plus.'
  website = Bintray.website
  issueTracker = Bintray.issueTracker
  repository = Bintray.repository
}
